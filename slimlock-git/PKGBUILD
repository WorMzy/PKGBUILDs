# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Joel Burget <joelburget@gmail.com>
# Contributor: Guff <kasull88@gmail.com>
# Contributor: Renato Aguiar
pkgname=slimlock-git
_gitname=slim
pkgver=v1.3.6.3.g741e439.2013.10.03
pkgrel=1
pkgdesc="Stand-alone lightweight themable screen locker based on SLiM (git)"
arch=('i686' 'x86_64')
url="http://programsthatsmellgood.com/slimlock/"
license=('GPL2')
depends=('libxft' 'imlib2' 'pam' 'libxrandr' 'libxmu' 'libpng' 'libjpeg')
makedepends=('git' 'cmake')
provides=('slimlock')
conflicts=('slimlock' 'slim>=1.3.6')
backup=('etc/slimlock.conf')
source=('git://git.berlios.de/slim'
        'slimlock-git.install'
        'slim.conf'
        'http://downloads.sourceforge.net/project/slim.berlios/slim-archlinux-simple.tar.gz'
        'slim-1.3.6-fix-libslim-libraries.patch')
install=slimlock-git.install
md5sums=('SKIP'
         '54f9c6598150b07841e9f64fb896403c'
         '3f75d2a7a85c08a41b2f75eeb548cd87'
         '7c7aaa255561444a4ae3e9f1dfb4be5d'
         '3b0579dccedff97bbeafa03d58666294')

pkgver() {
  cd "$_gitname"
  echo $(git describe --tags).$(git log -1 --format="%cd" --date=short) | sed 's:-:.:g'
}

prepare() {
  cd "$_gitname"
  patch -Np1 -i "$srcdir/slim-1.3.6-fix-libslim-libraries.patch"
}

build() {
  cd "$_gitname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_PAM=yes \
    -DUSE_CONSOLEKIT=no \
    -DBUILD_SHARED_LIBS=no
  make slimlock
}

package() {
  cd "$_gitname"

  install -Dm755 slimlock "$pkgdir/usr/bin/slimlock"
  install -Dm644 slimlock.conf "$pkgdir/etc/slimlock.conf"
  install -Dm644 slimlock.1 "$pkgdir/usr/share/man/man1/slimlock.1"

  install -D -m644 "$srcdir/slim.conf" "$pkgdir/etc/slim.conf"

  cd "$srcdir/slim-archlinux-simple"

  install -D -m644 slim.theme \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/slim.theme"
  install -D -m644 background.png \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/background.png"
  install -D -m644 panel.png \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/panel.png"
  install -D -m644 preview.png \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/preview.png"
}

# vim:set ts=2 sw=2 et:
