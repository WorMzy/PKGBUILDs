# Maintainer: RudolfPolzer <divVerent@xonotic.org>
# Based on the mplayer2-lite-git PKGBUILD

pkgname=mpv-git
pkgver=0.36446.84dcb76
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=('libpulse' 'ffmpeg' 'lcms2' 'libdvdread' 'libxinerama' 'libxss' 'desktop-file-utils' 'libquvi' 'libxv' 'libcdio-paranoia')
makedepends=('git' 'libgl' 'python-docutils')
provides=('mpv')
conflicts=('mpv')
options=(!emptydirs)
install=mpv-git.install
source=('git+http://github.com/mpv-player/mpv#branch=master'
        'mpv.desktop'
        'mpv-git.install')
md5sums=('SKIP'
         '5753c2001c302b1e43ddee1cf063c0bc'
         'cbc234e5e789e30c624741173992a225')

_gitname="mpv"

pkgver() {
  cd "$srcdir/$_gitname"
  microver="$(git log -1 --pretty=format:%h )"
  minorver="$(git rev-list --count HEAD)"
  echo "0.$minorver.$microver"
}

build() {
  # Custom CFLAGS break the mpv build
  unset CFLAGS
  unset LDFLAGS

  cd "$srcdir/$_gitname"

  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make -j1 DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/doc/mpv/examples

  # some extra useful stuff "make install" doesn't install
  install -m644 ${srcdir}/${_gitname}/etc/{input,example,encoding-example-profiles}.conf "${pkgdir}/usr/share/doc/mpv/examples"
  install -m644 ${srcdir}/${_gitname}/DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"

  # file association
  install -Dm644 "${srcdir}/mpv.desktop" "${pkgdir}/usr/share/applications/mpv.desktop"
}
