# Maintainer: Rudolf Polzer <divVerent@xonotic.org>
# Contributor:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-git
_gitname=mpv
pkgver=0.5.0_127_g3d968fc
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2 (git version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=('libpulse' 'ffmpeg' 'lcms2' 'libdvdread' 'libxinerama' 'libxss' 'desktop-file-utils' 'libquvi' 'libxv' 'libcdio-paranoia' 'hicolor-icon-theme')
makedepends=('git' 'libgl' 'python-docutils')
provides=('mpv')
conflicts=('mpv')
options=(!emptydirs)
install=mpv.install
source=('git+https://github.com/mpv-player/mpv#branch=master'
        'mpv.desktop')
md5sums=('SKIP'
         '5753c2001c302b1e43ddee1cf063c0bc')

pkgver() {
  cd "$srcdir/$_gitname"
  _curtag="$(git rev-list --tags --max-count=1)"
  _tagver="$(git describe --tags $_curtag | sed 's:^v::')"
  _commits="$(git rev-list --count HEAD --since=$_tagver)"
  _sha="$(git rev-parse --short HEAD)"
  printf "%s_%s_g%s" $_tagver $_commits $_sha
}

build() {
  cd "$srcdir/$_gitname"

  ./bootstrap.py
  ./waf configure --prefix=/usr --confdir=/etc/mpv 

  ./waf build
}

package() {
  cd "$srcdir/$_gitname"

  ./waf install --destdir="$pkgdir"

  # file association
  install -Dm644 "${srcdir}/mpv.desktop" "${pkgdir}/usr/share/applications/mpv.desktop"

  # example files
  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
  install -m755 TOOLS/mpv_identify.sh "$pkgdir"/usr/bin
}
