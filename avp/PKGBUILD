# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=avp
pkgver=20150214
pkgrel=1
pkgdesc="Alien Versus Predator Gold engine"
arch=(i686 x86_64)
url="https://www.icculus.org/avp"
license=('custom')
depends=('sdl' 'libgl' 'openal' 'glu')
makedepends=('cmake')
install='avp.install'
source=("${url}/files/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c485a55ff455498bcf82bc17f2056d35d4c646bdf7809f4c326fc7063a9ce5db9e4893af67dff3f6bedb7d5f96e82f792d7f516044fdbab638113091a5505158')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install binary
  install -D -m755 "avp" "$pkgdir/opt/avp/avp"
  install -d -m755 "$pkgdir/usr/bin/"

  # install readme
  install -D -m755 "README" "$pkgdir/opt/$pkgname/README"

  # install license
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # symlink binary to /usr/bin
  cd "$pkgdir/usr/bin/"
  ln -s /opt/avp/avp avp
}
