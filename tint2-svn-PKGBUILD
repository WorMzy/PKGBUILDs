# Contributor: zoulnix <http://goo.gl/HQaP>
# Maintainer: vicky91 <vickypaiers@gmail.com>
pkgname=tint2-svn
pkgver=652 
pkgrel=1
pkgdesc="A basic, good-looking task manager for WMs."
arch=('i686' 'x86_64')
url="http://code.google.com/p/tint2/"
license=('GPL')
depends=('libxcomposite' 'libxinerama' 'libxrandr' 'pango' 'imlib2')
makedepends=('cmake' 'subversion')
options=('!libtool')
provides=('tint2')
conflicts=('tint2' 'tint' 'ttm-svn')
source=("svn+http://tint2.googlecode.com/svn/trunk")
md5sums=('SKIP')

pkgver() {
  cd "$SRCDEST/trunk"
  svnversion | tr -d [A-z]
}

build() {
  cd "$srcdir/trunk"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
	  -DENABLE_TINT2CONF=0

  make
}

package() {
  cd "$srcdir/trunk"

  make DESTDIR="$pkgdir" install
}
