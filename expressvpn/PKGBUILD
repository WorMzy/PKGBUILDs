# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
pkgname=expressvpn
pkgver=1.0_1
pkgrel=1
pkgdesc="ExpressVPN client"
arch=('x86_64' 'i686')
url="http://www.expressvpn.com"
license=('custom: https://www.expressvpn.com/vpn-software/vpn-linux/open-source')
options=('emptydirs')
source=("expressvpnd.service" "license::https://www.expressvpn.com/vpn-software/vpn-linux/open-source")
source_x86_64=("https://d11cv8afjim9ws.cloudfront.net/clients/linux/${pkgname}-${pkgver/_/-}.x86_64.rpm"{,.asc})
source_i686=("https://d11cv8afjim9ws.cloudfront.net/clients/linux/${pkgname}-${pkgver/_/-}.i386.rpm"{,.asc})
md5sums=('153f2ea89821936c84b575a7b3c14b7b'
         '0436acac1a6cea1845a88748ed4bd854')
md5sums_x86_64=('2f25fe854ceabdfd2be631528fcba6a8'
                'SKIP')
md5sums_i686=('8fff2eda68a98a07bda7987684b975c4'
              'SKIP')
validpgpkeys=('1D0B09AD6C93FEE93FDDBD9DAFF2A1415F6A3A38')

package() {
  install -dm755 "$pkgdir/usr/bin"
  cp usr/{s,}bin/* "$pkgdir/usr/bin"
  cp -r usr/share "$pkgdir/usr"
  install -dm755 "$pkgdir/var/lib/expressvpn"
  install -Dm644 expressvpnd.service "$pkgdir/usr/lib/systemd/system/expressvpnd.service"

  install -Dm644 "$srcdir/license" "$pkgdir/usr/share/licenses/$pkgname/license"
}
