# Maintainer: Viech
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Martin F. Schumann <mfs@mfs.name>

pkgname=unvanquished
pkgver=0.19.0
pkgrel=1
pkgdesc="A team-based fps/rts hybrid game which pits aliens against humans. Monthly release that can be played on official servers."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp>=0.2.0' 'libtheora' 'nettle' 'speex' 'xvidcore' 'openal' 'xdg-utils' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme' 'libgl')
makedepends=('git' 'cmake')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished-git')
options=('emptydirs')
backup=('srv/unvanquished/main/server.cfg' 'srv/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=('unvanquished::git://github.com/Unvanquished/Unvanquished.git'
        'unvanquished.install'
        'unvanquished.sh'
        'unvanquished-server.sh'
        'unvanquished-update-paks.sh'
        'unvanquished.service'
        'unvanquished.conf'
        'unvanquished.desktop'
        'ChangeLog')

md5sums=('SKIP'
         'ce5398923fa900f2f18ff58aca7bbe35'
         'f049a8c71d6b1e3e8415f7f5ec348e17'
         'd25d57c778fc627a95f41b082cffef52'
         '1da9acdc7e8f83610dd6398ba2b38e72'
         'd272a70f58778affeabac13cc8a880da'
         '9d576da480e18b7f7352c49e537d2005'
         'ac69d49b3c665d274d0ab58870220522'
         '0a34737ce878f8335c205329da79ac5f')

# I'm using _name for compliance with the *-git version of this package
_name=$pkgname
_gitver="v$pkgver"

prepare() {
  cd "$srcdir/$_name"
  git checkout $_gitver
}

build() {
  cd "$srcdir/$_name"
  cmake -D CMAKE_INSTALL_PREFIX="$pkgdir/opt" . && make
}

package() {
  cd "$srcdir/$_name"
  make install

  # install docs
  for _file in COPYING.txt GPL.txt README.md; do
    install -m644 $_file "$pkgdir/opt/Unvanquished/${_file}"
  done

  # install scripts and service files
  cd "$srcdir"
  for _file in ${_name}.sh ${_name}-server.sh ${_name}-update-paks.sh; do
    install -m755 $_file "$pkgdir/opt/Unvanquished/${_file%.sh}"
  done
  install -Dm644 "${_name}.service" "$pkgdir/usr/lib/systemd/system/${_name}.service"
  install -Dm644 "${_name}.conf" "$pkgdir/etc/conf.d/${_name}.conf"
  install -Dm644 "${_name}.desktop" "$pkgdir/usr/share/applications/${_name}.desktop"
  cd $_name
  install -m755 download-pk3.sh "$pkgdir/opt/Unvanquished/download-pk3.sh"

  # install icon
  cd debian
  install -m644 ${_name}.png "$pkgdir/opt/Unvanquished/${_name}.png"

  # setup server directory 
  install -dm755 "$pkgdir/srv/$_name/main"
  cd config
  for _file in *; do
    install -m660 "$_file" "$pkgdir/srv/$_name/main/$_file"
  done
  
  cd "$pkgdir/srv/$_name/"

  ln -s . .Unvanquished

  #make cache dir
  install -d "$pkgdir/var/cache/$_name/update-paks"

  # rename directory to all lowercase for compatibility reasons
  cd "$pkgdir/opt"
  mv Unvanquished $_name

  # symlink scripts to bin dir
  install -dm755 "$pkgdir/usr/bin"
  cd $pkgdir/usr/bin/
  
  ln -s /opt/$_name/${_name} .
  ln -s /opt/$_name/${_name}-server .
  ln -s /opt/$_name/${_name}-update-paks .
}
