# Maintainer: Alexander De Sousa <archaur.xandy21@spamgourmet.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gtk-engine-murrine-git
_gitname=murrine
pkgver=406.2032a9b
pkgrel=1
pkgdesc="GTK2 Murrine engine, development code."
arch=('i686' 'x86_64')
license=('LGPLv3')
depends=('gtk2')
makedepends=('git' 'intltool')
url="http://cimitan.com/murrine/"
conflicts=('gtk-engine-murrine')
provides=('gtk-engine-murrine')
options=(!libtool)
source=("git://git.gnome.org/murrine"
        autotoolfix.patch)
md5sums=('SKIP'
         '526c52810848fef696084418e4fc239f')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $_gitname
  patch -p1 -i "$srcdir/autotoolfix.patch"
}

build() {
  cd $_gitname

  ./autogen.sh \
   --prefix=/usr \
   --enable-animation \
   --enable-animationrtl
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}

# vim: set ft=sh ts=2 sw=2 et:
