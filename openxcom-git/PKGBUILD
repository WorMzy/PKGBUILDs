# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Stefan Karner <stefan.karner at student.tuwien.ac.at>

pkgbase=openxcom-git
pkgname=('openxcom-git' 'openxcom-docs-git')
_gitname=OpenXcom
pkgver=1.0_259_gc048095
pkgrel=1
arch=('i686' 'x86_64')
url="http://openxcom.org/"
license=('GPL3')
makedepends=('git' 'boost' 'glu' 'xmlto' 'docbook-xml' 'docbook-xsl' 'doxygen'
             'sdl' 'sdl_gfx' 'sdl_image' 'sdl_mixer' 'yaml-cpp')
source=(git+"https://github.com/SupSuper/${_gitname}.git")
sha256sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --tags | sed -e 's:v::' -e 's:-:_:g'
}

prepare() {
  cd $_gitname
  ./autogen.sh
}

build() {
  cd $_gitname
  ./configure --prefix=/usr
  make
}

package_openxcom-git() {
  pkgdesc="An open-source reimplementation of the famous X-COM game (git-version)"
  depends=('libdrm' 'libgl' 'libx11' 'libxau' 'libxcb' 'libxdamage'
           'libxdmcp' 'libxext' 'libxfixes' 'libxshmfence' 'libxxf86vm'
           'mesa' 'sdl' 'sdl_gfx' 'sdl_image' 'sdl_mixer' 'yaml-cpp')
  provides=('openxcom')
  conflicts=('openxcom')
  install="${pkgname}.install"

  cd $_gitname

  # disable subdirs -- we install docs separately
  sed -i 's:^SUBDIRS = docs$::' Makefile

  make DESTDIR="$pkgdir" install
}

package_openxcom-docs-git() {
  pkgdesc="Documentation for the open-source reimplementation of the famous X-COM game (git-version)"
  arch=('any')
  cd $_gitname
  make -C docs DESTDIR="$pkgdir" install
}
