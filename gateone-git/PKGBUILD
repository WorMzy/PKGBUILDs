# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Ankz <ankz . kothari at gmail.com>
# Contributor: Figue <ffigue at gmail.com>

pkgname=gateone-git
_gitname="GateOne"
pkgver=v1.1_r333.g0aecd45
pkgrel=1
pkgdesc="Web-based terminal emulator and SSH client"
arch=('any')
url="https://github.com/liftoff/GateOne"
license=('AGPL3')
depends=('python2' 'python2-tornado' 'python2-futures' 'python2-setuptools')
optdepends=('python2-imaging' 'python2-pyopenssl' 'dtach')
makedepends=('git')
options=(emptydirs)
install=${pkgname}.install
source=(git+"https://github.com/liftoff/GateOne.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe | sed -e 's:-:_r:' -e 's:-:.:g'
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --root="$pkgdir/" --install-data="/usr/lib/python2.7/site-packages/"
  install -D scripts/init/gateone.service "$pkgdir/usr/lib/systemd/system/gateone.service"

  # create empty conf dir
  install -dm755 "$pkgdir/etc/gateone/conf.d"
}

# vim:set ts=2 sw=2 et:
