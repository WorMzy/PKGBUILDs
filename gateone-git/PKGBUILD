# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Ankz <ankz . kothari at gmail.com>
# Contributor: Figue <ffigue at gmail.com>
pkgname=gateone-git
_gitname="GateOne"
pkgver=v1.1_r332.gb414e57
pkgrel=1
pkgdesc="Web-based terminal emulator and SSH client"
arch=('any')
url="https://github.com/liftoff/GateOne"
license=('AGPL3')
depends=('python2-tornado' 'python2' 'python2-futures' 'python2-setuptools')
optdepends=('python2-imaging' 'python2-pyopenssl' 'dtach')
makedepends=('git')
options=(emptydirs)
install=${pkgname}.install
source=(git+"https://github.com/liftoff/GateOne.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe | sed -e 's:-:_r:' -e 's:-:.:g'
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --root="$pkgdir/"
  install -D ./scripts/init/gateone.service "$pkgdir/usr/lib/systemd/system/gateone.service"
  # fix file locations
  cd "$pkgdir/usr/gateone"
  for dir in *; do
    cp -r "$dir" "$pkgdir/usr/lib/python2.7/site-packages/gateone"
  done
  rm -r "$pkgdir/usr/gateone"

  # create empty conf dir
  install -dm755 "$pkgdir/etc/gateone/conf.d"
}

